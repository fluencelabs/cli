aqua Main

import "@fluencelabs/aqua-lib/builtin.aqua"
import "@fluencelabs/aqua-lib/workers.aqua"

use "hosts.aqua"
import "services.aqua"

export runDeployedServices, areAllWorkersRemoved

data Answer:
    answer: string
    peer: string

func runDeployedServices() -> *Answer:
    hosts <- Hosts.get()
    installationSpells = hosts.defaultWorker!.installationSpells
    answers: *Answer
    for s <- installationSpells:
        on s.workerId via s.hostId:
            answer <- NewService2.greeting("fluence")
            answers <<- Answer(answer=answer, peer=s.hostId)

    <- answers

func areAllWorkersRemoved() -> bool:
    hosts <- Hosts.get()
    installationSpells = hosts.defaultWorker!.installationSpells
    dummyDealId = hosts.defaultWorker!.dummyDealId

    allWorkersRemoved: ?bool

    for s <- installationSpells:
        on s.hostId:
            res <- DealWorker.get_worker_id(?[dummyDealId])

            if res != nil:
                allWorkersRemoved <<- false

    if allWorkersRemoved == nil:
        allWorkersRemoved <<- true

    <- allWorkersRemoved!
